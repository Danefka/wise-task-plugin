plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.2'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.openapi.generator' version '6.3.0'
    id "org.flywaydb.flyway" version "9.15.1"
    id "com.google.protobuf" version "0.9.2"
}

group = 'ru.leti'
version = '1.0.0'
sourceCompatibility = "19"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/resources/proto'
        }
        java {
            srcDir "${buildDir}/generated/source/src"
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/Terross/wise-task-library")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME") //ghp_qjIOixTAD9J TzVc2aXZeXXCVIleU8o09rCot
            password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
        }
    }
}

dependencies {

    //grpc dependencies
    implementation 'io.github.lognet:grpc-spring-boot-starter:5.0.0'
    implementation 'io.grpc:grpc-protobuf:1.53.0'
    implementation 'io.grpc:grpc-stub:1.53.0'
    implementation 'com.google.protobuf:protobuf-java-util:3.21.7'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'ru.leti:wise-task-library:1.0.3'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.postgresql:postgresql:42.2.27'

    implementation 'commons-codec:commons-codec:1.15'

    implementation 'org.mapstruct:mapstruct:1.5.3.Final'

    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'
    annotationProcessor 'no.entur.mapstruct.spi:protobuf-spi-impl:1.42'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    //OpenAPI dependencies
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.14.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.2'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.4'
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.0.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.14'
    implementation 'io.swagger:swagger-annotations:1.6.8'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'javax.servlet:javax.servlet-api:4.0.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    compileOnly 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.7"
    }
    clean {
        delete protobuf.generatedFilesBaseDir
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.53.0"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}


//OpenAPI
openApiGenerate {
    generatorName = 'spring'
    inputSpec = "$rootDir/src/main/resources/specs/wise-task-plugin-api.yaml".toString()
    outputDir = "$buildDir/generated/source".toString()
    apiPackage = "ru.leti.wise.task.plugin.api"
    modelPackage = "ru.leti.wise.task.plugin.model"
    generateModelTests = false
    generateApiTests = false
    generateAliasAsModel = true
    configOptions = [
            delegatePattern: "true"
    ]
}

tasks.openApiGenerate {
    doLast {
        delete (
                "$buildDir/generated/source/pom.xml",
                "$buildDir/generated/source/.openapi-generator-ignore",
                "$buildDir/generated/source/README.md",
                "$buildDir/generated/source/.openapi-generator",
                "$buildDir/generated/source/src/test/",
                "$buildDir/generated/source/src/main/java/org",
        )
    }
}

//Migration
flyway {
    url = 'jdbc:postgresql://localhost:5432/wise_task_plugin'
    user = 'wise_task_plugin'
    password = 'wise_task_plugin'
    cleanDisabled = false
    locations = ['filesystem:./src/main/resources/db/migration']
}


compileJava.dependsOn tasks.openApiGenerate

tasks.named('test') {
    useJUnitPlatform()
}

compileJava {
    options.compilerArgs += ['--enable-preview']
}

bootRun {
    jvmArgs(['--enable-preview'])
}